plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.12.0'
}

apply from: 'build.gradle'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

def ossrhUsername = hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('OSSRH_USERNAME')
def ossrhPassword = hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('OSSRH_PASSWORD')

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

nexusStaging {
    packageGroup = "com.launchdarkly"
    username = ossrhUsername
    password = ossrhPassword
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'api-client'
            artifact sourceJar
            artifact javadocJar

            pom {
                  name = "API Client for Java"
                  packaging = 'jar'
                  description = 'Official LaunchDarkly API Client for Java'
                  url = 'https://github.com/launchdarkly/api-client-java'

                  licenses {
                      license {
                          name = 'The Apache License, Version 2.0'
                          url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                      }
                  }

                  developers {
                      developer {
                          id = 'launchdarkly'
                          name = 'LaunchDarkly'
                          email = 'support@launchdarkly.com'
                      }
                  }

                  scm {
                      connection = 'scm:git:git://github.com/launchdarkly/api-client-java.git'
                      developerConnection = 'scm:git:ssh:git@github.com:launchdarkly/api-client-java.git'
                      url = 'https://github.com/launchdarkly/api-client-java'
                  }
           }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
